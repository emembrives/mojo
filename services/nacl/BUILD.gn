# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")

# Trusted code
if (!is_nacl) {
  mojo_native_application("nacl_content_handler") {
    sources = [
      "content_handler_main.cc",
    ]

    deps = [
      "//base",
      "//mojo/application:application",
      "//mojo/application:content_handler",
      "//mojo/data_pipe_utils",
      "//mojo/message_pump",
      "//mojo/public/platform/native:system_impl_private",
      "//mojo/services/network/public/interfaces",
      "//nacl_bindings:monacl_sel",
      "//url",
    ]

    data_deps = [ "//nacl_bindings:irt_mojo(//native_client/build/toolchain/nacl:irt_${target_cpu})" ]
  }

  mojo_native_application("nacl_content_handler_nonsfi") {
    sources = [
      "content_handler_main_nonsfi.cc",
    ]

    deps = [
      "//base",
      "//mojo/application:application",
      "//mojo/application:content_handler",
      "//mojo/data_pipe_utils",
      "//mojo/message_pump",
      "//mojo/nacl:irt_mojo_nonsfi",
      "//mojo/public/cpp/application:application",
      "//native_client/src/nonsfi/loader:elf_loader",
    ]
  }
}
